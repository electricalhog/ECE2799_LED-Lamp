.SUBCKT FDMQ8203_Q1Q4_N 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDMQ8203 Spice model                    **
**                    Revision RevA, 26 July 2011               **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD 
Dbreak 5 11 DbreakMOD 
Lgate 1 9 1.503e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.521e-9
RLgate 1 9 15.03
RLdrain 2 5 1
RLsource 3 7 5.21
Rgate 9 6 6.11

* Shielded  Gate  
D_D1 100 5 D_SG_cap
D_D2 100 101 D_SG_cap
R_R1 101 7 6.58
C_C1 6 101 16e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=5e-3 CJO=0.23e-9 M=0.54 t_abs=25) 

It 7 17 1
Ebreak 11 7 17 7 110.75
Rbreak 17 7 RbreakMOD 1 
.MODEL RbreakMOD RES (TC1=0.69e-3 TC2=-0.25e-6)
.MODEL DbodyMOD D (IS=1e-12 n=1.05 RS=23.5e-3 TRS1=1.5e-3 TRS2=1e-6 
+ CJO=0.06e-9 M=0.4 TT=1e-9 XTI=2.75)
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 3.445e-3
Rdrain 5 16 RdrainMOD 60.0e-3
.MODEL RdrainMOD RES (TC1=6.45e-3 TC2=19e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=0.37 L=1.15e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1000e-10
+ XJ=0.62e-6
+ NCH=0.96e17
*Channel Current
+ U0=670 VSAT=500000 DROUT=1.8
+ DELTA=0.05 PSCBE2=0 RSH=3.445e-3
*Threshold voltage
+ VTH0=3.25
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.4
*Junction diodes and Capacitance
+ LINT=0.175e-6 DLC=0.175e-6 
+ CGSO=174e-12 CGSL=0 CGDO=0.5e-12 CGDL=155e-12 
+ CJ=0 CF=0 CKAPPA=0.8
* Temperature parameters 
+ KT1=-2.1 KT2=0 UA1=4.75e-9
+ NJ=10)
.ENDS   
* 
*
.SUBCKT FDMQ8203_Q2Q3_P 2 1 3
*Nom Temp 25 deg C		
Dbody 5 7 DbodyMOD 		
Dbreak 7 11 DbreakMOD 		
Lgate 1 9 0.559e-9		
Ldrain 2 5 0.1e-9		
Lsource 3 7 0.281e-9		
RLgate 1 9 5.59		
RLdrain 2 5 1		
RLsource 3 7 2.81		
Rgate 9 6 1.48		
It 7 17 1		
Ebreak 5 11 17 7 -90		
Rbreak 17 7 RbreakMOD 1 		
.MODEL RbreakMOD RES (TC1=0.95e-3 TC2=-0.2e-6)		
.MODEL DbodyMOD D (IS=0.67e-12 n=1 RS=28e-3 TRS1=0.4e-3 TRS2=4e-6 		
+ CJO=0.01e-9 M=0.65 TT=3e-9 XTI=2.6)		
.MODEL DbreakMOD D (RS=0 TRS1=65e-3 TRS2=300e-6 )		
Rsource 7a 7 4.467e-3		
Rdrain 5 16 RdrainMOD 150e-3		
.MODEL RdrainMOD RES (TC1=6.1e-3 TC2=8.8e-6)		
M_BSIM3 16 6 7a 7a Bsim3 W=0.66 L=1.7e-6 NRS=0 NRD=0		
.MODEL Bsim3 PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0		
*Process Parameters		
+ TOX=410e-10
+ XJ=1.6ue-6	
+ NCH=1.5e17	
*Channel Current		
+ U0=420 VSAT=100000 DROUT=1.8		
+ DELTA=0.7 PSCBE2=0.00001 RSH=4.467e-3		
*Threshold voltage		
+ VTH0=-1.76		
*Sub-threshold characteristics		
+ VOFF=-0.21 NFACTOR=1.0		
*Junction diodes and Capacitance		
+ LINT=0.4e-6 DLC=0.4e-6 		
+ CGSO=330e-12 CGSL=0 CGDO=20e-12 CGDL=700e-12 		
+ CJ=0 CF=0 CKAPPA=1
* Temperature parameters 		
+ KT1=-1.1 KT2=0 UA1=7.0e-9		
+ NJ=10)		
.ENDS   		
